%Dati in input:
%giocatore(nome,soldi,isPrigione(),numToken,turniPrigione,posizione)
%
%Sets posseduti:
%set(colore,prezzoVendita1Casa)
%
%Caselle non ipotecate:
%casella(nome,prezzoIpoteca)

%Posso vendere case o ipotecare
vendi(X) | nonVendi(X) :- set(X,_).
ipoteca(X) | nonIpoteca(X) :- casella(X,_).
ipoteca(X) | nonIpoteca(X) :- casellaRes(X,_).

%Vendo o ipoteco che sia, massimo una sola (casella o set case)
%numVendi(X) :- X = #count{Set, Z:vendi(Set)}.
:-  #count{Set:vendi(Set)} > 1.
:-  #count{Casella:ipoteca(Casella)} > 1.
:- #count{Casella:ipoteca(Casella)} = 1, #count{Set:vendi(Set)} = 1.

%Se sono qui, non posso non ipotecare/vendere niente
:- #count{Casella:ipoteca(Casella)} = 0, #count{Set:vendi(Set)} = 0.

%Preferisco ipotecare le caselle non residenziali, poi le residenziali
%e infine vendere case
:~ ipoteca(Casella), casellaRes(Casella,X). [1:1]
:~ vendi(Set). [2:1]

%Totale soldi fatti
tot(X) :- vendi(Set), set(Set,X).
tot(X) :- ipoteca(Casella), casella(Casella,X).
tot(X) :- ipoteca(Casella), casellaRes(Casella,X).

%Preferisco vendere/ipotecare quello con meno valore
:~ tot(X). [X:2]